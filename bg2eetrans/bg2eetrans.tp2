BACKUP ~bg2eetrans/backup~
AUTHOR ~Isaya~

// Version stated in WeiDU.log
// The last part refers to the game version used as a reference to find texts
VERSION ~v0.7.16.6~

//README ~bg2eetrans/language/%LANGUAGE%/readme_%LANGUAGE%.txt~ ~bg2eetrans/language/readme_en_US.txt~
README ~bg2eetrans/readme-%LANGUAGE%.html~

// Tells WeiDU where the tra files are, according to language choice (%s refers to the language name)
AUTO_TRA ~bg2eetrans/language/%s~
// Comment to prevent a failure in the notepad++ highlither because of the ~%~ character above

ALWAYS

END	// ALWAYS


// Language choice : follow the rule, use the directory names from BGEE
// ATTENTION!!! The order of languages here and in bg2eetransinstall.tp2 shall remain consistent at all time
// It shall remain consistant as well with the values assigned to languages at the end of this file
// 0 is the first language choice, 1 the next, and so on
// Languages in comments do not count!
LANGUAGE ~Francais~
	     ~fr_FR~
         ~bg2eetrans/language/fr_FR/setup.tra~


// Initialisation 
BEGIN @1000001

// We need BG2EE and we don't want to overwrite any official translation.
// Hence the mod does not install if the dialog.tlk file already exists,
// even if it doesn't come from the official translation (there's no way to know).
REQUIRE_PREDICATE (GAME_IS ~bg2ee~ = 1) @1001001 // Wrong game

REQUIRE_PREDICATE (NOT FILE_EXISTS ~lang/%LANGUAGE%/dialog.tlk~) @1001002 // Translation file already present

// Create the language directory if it does not exist
ACTION_IF NOT FILE_EXISTS ~lang/%LANGUAGE%~ THEN BEGIN
	MKDIR ~lang/%LANGUAGE%~
END
COPY ~lang/en_US/dialog.tlk~ ~lang/%LANGUAGE%/dialog.tlk~
COPY ~lang/en_US/dialog.tlk~ ~lang/%LANGUAGE%/dialogF.tlk~
// MKDIR ~lang/%LANGUAGE%/override~

// Addition to BGEE.lua moved to bg2eetransinstall.tp2 in order to add translation notes in the right language

COPY ~bg2eetrans/empty.itm~ ~lang/%LANGUAGE%/bg2eetrans.mrk~

// Overwrite weidu.conf to specify the chosen language so that the other components are installed properly
<<<<<<<< .../bg2eetrans-inlined/weidu.conf
lang_dir = %lang%
>>>>>>>>
COPY ~.../bg2eetrans-inlined/weidu.conf~ ~weidu.conf~
	SPRINT lang ~%LANGUAGE%~
	EVALUATE_BUFFER

// Ensure that tests and value assignment follow the order in the LANGUAGE instructions above
ACTION_IF (~%LANGUAGE%~ STRING_EQUAL_CASE ~fr_FR~) THEN BEGIN
	OUTER_SET NUMLANG = 0
END
// Add addtional language here, increment NUMLANG for each new language

// Display a text explaining that installation will be long, in order to avoid people
// assume installation is over this first part of the mod is successfully done.
PRINT @1000501

// Run the installation in the selected language
// Note: no auto update since it may trigger an error message and running the current tp2 should already have done it anyway
// Check that we are running installation: bg2eetrans.mrk file should exist
ACTION_IF (FILE_EXISTS ~lang/%LANGUAGE%/bg2eetrans.mrk~) THEN BEGIN
  ACTION_IF ("%WEIDU_OS%" STRING_COMPARE_CASE ~WIN32~ = 0) THEN BEGIN
    AT_INTERACTIVE_EXIT ~setup-bg2eetrans bg2eetrans/bg2eetransinstall.tp2 --force-install-list 1001 1002 1003 1004 --language %NUMLANG% --noautoupdate~
    END ELSE BEGIN
      // Anticipate that . may not be in the PATH on Linux or Mac OS X (zelurker feedback on Beamdog forum)
      AT_INTERACTIVE_EXIT ~./setup-bg2eetrans bg2eetrans/bg2eetransinstall.tp2 --force-install-list 1001 1002 1003 1004 --language %NUMLANG% --noautoupdate~
    END
  END // FILE_EXISTS

// Uninstall instructions
// REQUIRE_COMPONENT used in components in bg2eetransinstall.tp2 already force uninstallation
// when this initialisation component is uninstalled, so there is no need to force uninstallation
// by calling bg2eetransinstall.tp2
/*
ACTION_IF ("%WEIDU_OS%" STRING_COMPARE_CASE ~WIN32~ = 0) THEN BEGIN
    AT_UNINSTALL ~setup-bg2eetrans bg2eetrans/bg2eetransinstall.tp2 --force-uninstall-list 1004 1003 1002 1001 --noautoupdate~
  END ELSE BEGIN
    AT_UNINSTALL ~./setup-bg2eetrans bg2eetrans/bg2eetransinstall.tp2 --force-uninstall-list 1004 1003 1002 1001 --noautoupdate~
  END
*/
// Since dialog.tlk and dialogF.tlk always come back after components of bg2eetransinstall.tp2
// are installed, make sure to delete them (otherwise their presence will prevent reinstallation)
ACTION_IF ("%WEIDU_OS%" STRING_COMPARE_CASE ~WIN32~ = 0) THEN BEGIN
	// Note: can't delete directory with options /S /Q as / are obsviously converted to \ by WeiDU
    AT_UNINSTALL_EXIT ~del lang/%LANGUAGE%/dialog.tlk lang/%LANGUAGE%/dialogF.tlk~
  END ELSE BEGIN
    AT_UNINSTALL_EXIT ~rm -rf "lang/%LANGUAGE%"~
  END
